# Maintainer: Magnus Therning <magnus@therning.org>

_pkgname=haskell-ide-engine
_stack_yaml_file=stack-8.6.3.yaml
pkgname=${_pkgname}-git
pkgver=r2325.ce022201
pkgrel=1
pkgdesc="The engine for Haskell ide-integration. Not an IDE."
arch=('x86_64')
url='https://github.com/haskell/haskell-ide-engine'
license=('custom:BSD3')
makedepends=('cabal-install' 'git' 'happy' 'stack')
depends=()
options=('strip' 'staticlibs')
source=("git://github.com/haskell/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"

  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_pkgname}"

  git submodule update --init

  stack --stack-yaml=${_stack_yaml_file} update
  stack --stack-yaml=${_stack_yaml_file} setup
}

build() {
  cd "${srcdir}/${_pkgname}"

  stack --stack-yaml=${_stack_yaml_file} build --dependencies-only
  stack --stack-yaml=${_stack_yaml_file} build hie-plugin-api
  stack --stack-yaml=${_stack_yaml_file} \
        exec ghc-pkg -- list | grep pkgdb | awk '{print "--package-db=" $0}' > db-paths
  stack --stack-yaml=${_stack_yaml_file} \
        exec --no-ghc-package-path cabal -- v1-configure \
        $(cat db-paths) \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
  stack --stack-yaml=${_stack_yaml_file} \
        exec --no-ghc-package-path cabal -- v1-build
}

check() {
  true
}

package() {
  cd "${srcdir}/${_pkgname}"

  stack --stack-yaml=${_stack_yaml_file} \
        exec --no-ghc-package-path cabal -- v1-copy \
        --destdir="${pkgdir}"

  rm -fr "${pkgdir}/usr/lib"
}
