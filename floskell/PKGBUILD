# Maintainer: Magnus Therning <magnus@therning.org>

pkgname=floskell
pkgver=0.9.0
pkgrel=100
pkgdesc="A flexible Haskell source code pretty printer"
arch=('x86_64')
url='https://hackage.haskell.org/package/floskell'
license=('BSD3')
makedepends=('cabal-install' 'stack')
depends=()
options=('strip' 'staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'stack.yaml')
sha256sums=('5badda565b0767d834a2ac2eb4fe431ad5a6779355e9e6c95974bdf70c0da67f'
            '5d3616354548db998d3aefa622f5d31d32f1840fddfeb00704fc08f9bc3a7df8')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp "${srcdir}/stack.yaml" .

  stack update
  stack setup
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack build --dependencies-only
  stack \
        exec ghc-pkg -- list | grep pkgdb | awk '{print "--package-db=" $0}' > db-paths
  stack \
        exec --no-ghc-package-path cabal -- v1-configure \
        $(cat db-paths) \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
  stack \
        exec --no-ghc-package-path cabal -- v1-build
}

check() {
  true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack \
    exec --no-ghc-package-path cabal -- v1-copy \
    --destdir="${pkgdir}"

  rm -fr "${pkgdir}/usr/lib"
}
