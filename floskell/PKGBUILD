# Maintainer: Magnus Therning <magnus@therning.org>

_pkgname=floskell
pkgname=${_pkgname}-git
pkgver=r654.4277898
pkgrel=1
pkgdesc="A flexible Haskell source code pretty printer"
arch=('x86_64')
url='https://github.com/ennocramer/floskell.git'
license=('custom:BSD3')
makedepends=('cabal-install' 'git' 'stack')
depends=()
options=('strip' 'staticlibs')
source=("git://github.com/ennocramer/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"

  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_pkgname}"

  git submodule update --init

  stack update
  stack setup
}

build() {
  cd "${srcdir}/${_pkgname}"

  stack build --dependencies-only
  stack \
        exec ghc-pkg -- list | grep pkgdb | awk '{print "--package-db=" $0}' > db-paths
  stack \
        exec --no-ghc-package-path cabal -- v1-configure \
        $(cat db-paths) \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
  stack \
        exec --no-ghc-package-path cabal -- v1-build
}

check() {
  true
}

package() {
  cd "${srcdir}/${_pkgname}"

  stack \
        exec --no-ghc-package-path cabal -- v1-copy \
        --destdir="${pkgdir}"

  rm -fr "${pkgdir}/usr/lib"
}
