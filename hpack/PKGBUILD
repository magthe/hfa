# Maintainer: Magnus Therning <magnus@therning.org>

pkgname=hpack
pkgver=0.27.0
pkgrel=1
pkgdesc="An alternative format for Haskell packages"
arch=('x86_64')
url='https://github.com/sol/hpack'
license=('BSD3')
makedepends=('cabal-install' 'stack')
depends=()
options=('strip' 'staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "stack.yaml"
        "ghc-8.0.2-settings"
       )
sha256sums=('d54744d77b645eb61d9498409904fca909c6d994402e806ad3c9c65596702bef'
            '2009d132d39917601a6eeb9e17166d9ad43a8b8e1f88d1b4875a2d76cc57cb2e'
            '0c9eabde6e281f2746789c108711d29273e3fba14148ac7676ceecee77478785')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp "${srcdir}/stack.yaml" .
  stack update
  stack setup

  # WTF??? see https://github.com/commercialhaskell/stack/issues/3648
  cp ${srcdir}/ghc-8.0.2-settings ${HOME}/.stack/programs/x86_64-linux/ghc-tinfo6-nopie-8.0.2/lib/ghc-8.0.2/settings
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack build --dependencies-only
  stack \
        exec ghc-pkg -- list | grep pkgdb | awk '{print "--package-db=" $0}' > db-paths
  stack \
        exec --no-ghc-package-path cabal -- configure \
        $(cat db-paths) \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
  stack \
        exec --no-ghc-package-path cabal -- build
}

check() {
  true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack \
        exec --no-ghc-package-path cabal -- copy \
        --destdir="${pkgdir}"

  rm -fr "${pkgdir}/usr/lib"
}
