# Maintainer: Magnus Therning <magnus@therning.org>

pkgname=happy
pkgver=1.19.10
pkgrel=100
pkgdesc="Happy is a parser generator for Haskell"
arch=('x86_64')
url='https://www.haskell.org/happy'
license=('BSD3')
makedepends=('cabal-install' 'stack')
depends=()
options=('strip' 'staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "stack.yaml"
       )
sha256sums=('22eb606c97105b396e1c7dc27e120ca02025a87f3e44d2ea52be6a653a52caed'
            'a0c543d351d7f22e9e8d6bc6334f4879c5cdbf13a602f76c9d15582accf99687')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp "${srcdir}/stack.yaml" .
  stack update
  stack setup
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack build --dependencies-only
  stack \
        exec ghc-pkg -- list | grep pkgdb | awk '{print "--package-db=" $0}' > db-paths
  stack \
        exec --no-ghc-package-path cabal -- configure \
        $(cat db-paths) \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
  stack \
        exec --no-ghc-package-path cabal -- build
}

check() {
  true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack \
        exec --no-ghc-package-path cabal -- copy \
        --destdir="${pkgdir}"

  rm -fr "${pkgdir}/usr/lib"
}
