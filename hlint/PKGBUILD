# Maintainer: Magnus Therning <magnus@therning.org>

pkgname=hlint
pkgver=2.2.3
pkgrel=100
pkgdesc="Source code suggestions"
arch=('x86_64')
url='http://www.haskell.org/cabal/'
license=('BSD3')
makedepends=('stack')
depends=()
options=('strip' 'staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
       "stack.yaml")
sha256sums=('e6df9f402a02011100248ffd4b9a5aa4bb5fbf7bb61677c50a0cdf5ac298b32a'
            'cd8a2ab72878bc378ca9182c9e4828e1ddbde2140126ed343816b6ad95e96f55')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ln -f -s "${srcdir}/stack.yaml"
  stack update
  stack setup
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack build --dependencies-only
  stack \
        exec ghc-pkg -- list | grep pkgdb | awk '{print "--package-db=" $0}' > db-paths
  stack \
    runhaskell --no-ghc-package-path -- Setup.hs configure \
    $(cat db-paths) \
    --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
  stack \
    runhaskell --no-ghc-package-path -- Setup.hs build
}

check() {
  true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack \
        runhaskell --no-ghc-package-path -- Setup.hs copy \
        --destdir="${pkgdir}"

  rm -fr "${pkgdir}/usr/lib"
}
