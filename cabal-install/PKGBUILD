# Maintainer: Magnus Therning <magnus@therning.org>

pkgname=cabal-install
pkgver=2.4.1.0
pkgrel=101
pkgdesc="The command-line interface for Cabal and Hackage."
arch=('x86_64')
url='http://www.haskell.org/cabal/'
license=('BSD3')
makedepends=('stack')
options=('strip' 'staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "stack.yaml"
       )
sha256sums=('69bcb2b54a064982412e1587c3c5c1b4fada3344b41b568aab25730034cb21ad'
            '531a4382af9047f9bda0f5bb26adfe1c95f4a461a508995e4d2a01d7a0cb7e88')

prepare() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp ${srcdir}/stack.yaml .
  stack update
  stack setup
}

build() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack build --dependencies-only
  stack \
        exec ghc-pkg -- list | grep pkgdb | awk '{print "--package-db=" $0}' > db-paths
  stack \
        runhaskell --no-ghc-package-path -- Setup.hs configure \
        $(cat db-paths) \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
  stack \
        runhaskell --no-ghc-package-path -- Setup.hs build
}

check() {
  true
}

package() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack \
        runhaskell --no-ghc-package-path -- Setup.hs copy \
        --destdir="${pkgdir}"

  chmod a+r "${pkgdir}/usr/share/man/man1/cabal.1"
}
