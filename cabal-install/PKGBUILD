# Maintainer: Magnus Therning <magnus@therning.org>

_stackver=v1.7.1
_stackname=stack-1.7.1-linux-x86_64
pkgname=cabal-install
pkgver=2.4.0.0
pkgrel=100
pkgdesc="The command-line interface for Cabal and Hackage."
arch=('x86_64')
url='http://www.haskell.org/cabal/'
license=('BSD3')
makedepends=('ncurses5-compat-libs')
options=('strip' 'staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "https://github.com/commercialhaskell/stack/releases/download/${_stackver}/${_stackname}.tar.gz"
        "stack.yaml"
       )
sha256sums=('1329e9564b736b0cfba76d396204d95569f080e7c54fe355b6d9618e3aa0bef6'
            '647306bbf65dd0b555486e2a32776ba1cfe0c8f380e31c6aa93abd452cb209ee'
            '339b30767173b6caa045b49dc08d4d3ecbe326ee5e0b5336e310f799f6b4dc4c')

prepare() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp ${srcdir}/stack.yaml .
  stack update
  stack setup
}

build() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack build
  stack \
        exec ghc-pkg -- list | grep pkgdb | awk '{print "--package-db=" $0}' > db-paths
  stack \
        exec --no-ghc-package-path cabal -- configure \
        $(cat db-paths) \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
  stack \
        exec --no-ghc-package-path cabal -- build
}

check() {
  true
}

package() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack \
        exec --no-ghc-package-path cabal -- copy \
        --destdir="${pkgdir}"

  chmod a+r "${pkgdir}/usr/share/man/man1/cabal.1"
}
