# Maintainer: Magnus Therning <magnus@therning.org>

pkgname=pandoc-crossref
pkgver=0.3.4.1
pkgrel=100
pkgdesc="Pandoc filter for cross-references"
arch=('x86_64')
url='http://www.haskell.org/cabal/'
license=('GPL2')
makedepends=('stack' 'unzip')
depends=('pandoc')
options=('strip' 'staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "stack.yaml")
sha256sums=('de22767a19cb8b58fb77be4e85e05040e68830b37eb565afa6f63c8da7221aa6'
            '69ea4173140f9a4033c9cfdd25a030e7473f91087326dce034266b29f2d00651')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp "${srcdir}/stack.yaml" .
  stack update
  stack setup
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack build --dependencies-only
  stack \
        exec ghc-pkg -- list | grep pkgdb | awk '{print "--package-db=" $0}' > db-paths
  stack \
        runhaskell --no-ghc-package-path -- Setup.hs configure \
        $(cat db-paths) \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
  stack \
        runhaskell --no-ghc-package-path -- Setup.hs build
}

check() {
  true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack \
        runhaskell --no-ghc-package-path -- Setup.hs copy \
        --destdir="${pkgdir}"

  rm -fr "${pkgdir}/usr/lib"
}
