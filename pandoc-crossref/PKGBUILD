# Maintainer: Magnus Therning <magnus@therning.org>

pkgname=pandoc-crossref
pkgver=0.3.6.1
pkgrel=100
pkgdesc="Pandoc filter for numbering figures, equations, tables and cross-references to them"
arch=('x86_64')
url='http://www.haskell.org/cabal/'
license=('GPL2')
makedepends=('stack' 'unzip')
depends=('pandoc')
options=('strip' 'staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "stack.yaml")
sha256sums=('b55c163dda01f5963d04cc233ac636c778322ea14f63e2646daf74981e3560ba'
            '70e5886671aeaea447ec4451f5d40d3c87cb152030f2d52b6f2cb7928db0806a')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp "${srcdir}/stack.yaml" .
  stack update
  stack setup
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack build --dependencies-only
  stack \
        exec ghc-pkg -- list | grep pkgdb | awk '{print "--package-db=" $0}' > db-paths
  stack \
        runhaskell --no-ghc-package-path -- Setup.hs configure \
        $(cat db-paths) \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
  stack \
        runhaskell --no-ghc-package-path -- Setup.hs build
}

check() {
  true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack \
        runhaskell --no-ghc-package-path -- Setup.hs copy \
        --destdir="${pkgdir}"

  rm -fr "${pkgdir}/usr/lib"
}
