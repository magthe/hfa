# Maintainer: Magnus Therning <magnus@therning.org>

pkgname=dhall-json
pkgver=1.6.1
pkgrel=100
pkgdesc="Convert between Dhall and JSON or YAML"
arch=('x86_64')
url='https://dhall-lang.org/'
license=('BSD3')
makedepends=('stack')
depends=()
options=('strip' 'staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
       "stack.yaml")
sha256sums=('3ce9b0a9d3a946beb021bb42589426ceb4c44cf5f104e5bdf120659ccb5109c9'
            '33ca8b3c8f06f2bec1ca1f935993fd23c3f6353cfc9001e2324f3ebe2173c660')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ln -f -s "${srcdir}/stack.yaml"
  stack update
  stack setup
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack build --dependencies-only
  stack \
        exec ghc-pkg -- list | grep pkgdb | awk '{print "--package-db=" $0}' > db-paths
  stack \
    runhaskell --no-ghc-package-path -- Setup.hs configure \
    $(cat db-paths) \
    --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
  stack \
    runhaskell --no-ghc-package-path -- Setup.hs build
}

check() {
  true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack \
        runhaskell --no-ghc-package-path -- Setup.hs copy \
        --destdir="${pkgdir}"

  rm -fr "${pkgdir}/usr/lib"
}
