# Maintainer: Magnus Therning <magnus@therning.org>

_stackver=v1.7.1
_stackname=stack-1.7.1-linux-x86_64
pkgname=stack
pkgver=1.7.1
pkgrel=100
pkgdesc="The Haskell Tool Stack - the binary"
arch=('x86_64')
url='http://www.haskell.org/cabal/'
license=('BSD3')
makedepends=('cabal-install' 'unzip')
depends=('ncurses5-compat-libs')
options=('strip' 'staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "https://github.com/commercialhaskell/stack/releases/download/${_stackver}/${_stackname}.tar.gz"
       )
sha256sums=('19c4f2e02975bb797a339cfe2893c9e1f40241a910da45be34c5c2f05d62329f'
            '647306bbf65dd0b555486e2a32776ba1cfe0c8f380e31c6aa93abd452cb209ee')

prepare() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack update
  stack setup
}

build() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack build --dependencies-only
  stack \
        exec ghc-pkg -- list | grep pkgdb | awk '{print "--package-db=" $0}' > db-paths
  stack \
        exec --no-ghc-package-path cabal -- configure \
        $(cat db-paths) \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
  stack \
        exec --no-ghc-package-path cabal -- build
}

check() {
  true
}

package() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack \
        exec --no-ghc-package-path cabal -- copy \
        --destdir="${pkgdir}"

  rm -fr "${pkgdir}/usr/lib"

  install -d -m755 "${pkgdir}/etc/bash_completion.d"
  ./dist/build/stack/stack --bash-completion-script /usr/bin/stack > stack_completion_script
  install -m644 stack_completion_script "${pkgdir}/etc/bash_completion.d/stack"
}
