# Maintainer: Magnus Therning <magnus@therning.org>

_stackver=v2.1.1
_stackname=stack-2.1.1-linux-x86_64-static
pkgname=stack
pkgver=2.1.1
pkgrel=100
pkgdesc="The Haskell Tool Stack - the binary"
arch=('x86_64')
url='http://www.haskell.org/cabal/'
license=('BSD3')
makedepends=('git' 'unzip')
depends=('ncurses5-compat-libs')
options=('strip' 'staticlibs')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/commercialhaskell/stack/archive/${_stackver}.tar.gz"
        "https://github.com/commercialhaskell/stack/releases/download/${_stackver}/${_stackname}.tar.gz"
       )
sha256sums=('10c0119456748b189724ee42dea093c324d101487c3d75d57eb625675bb57424'
            'fffb6996312be9c45227aea2fbde4b0ed71c9e9cb52aeefb78f585e3ddd14e9f')

prepare() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack update
  stack setup
}

build() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  stack --local-bin-path stack-bins install
}

check() {
  true
}

package() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -d -m755 "${pkgdir}/usr/bin"
  install -m755 stack-bins/stack "${pkgdir}/usr/bin/stack"

  install -d -m755 "${pkgdir}/usr/share/dock/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/dock/${pkgname}/LICENSE"

  # Shell completion?
  # stack --bash-completion-script /usr/bin/stack
  # stack --zsh-completion-script /usr/bin/stack
}
