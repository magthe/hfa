# Maintainer: Magnus Therning <magnus@therning.org>

_stackver=v2.1.1
_stackname=stack-2.1.1-linux-x86_64-static
_ghorg=commercialhaskell
_commit=388582c
pkgname=stack
pkgver=2.1.1.1
pkgrel=100
pkgdesc="The Haskell Tool Stack - the binary"
arch=('x86_64')
url='http://www.haskell.org/cabal/'
license=('BSD3')
makedepends=('git' 'unzip')
depends=('ncurses5-compat-libs')
options=('strip' 'staticlibs')
source=("${pkgname}-${pkgver}.tar.gz::https://api.github.com/repos/${_ghorg}/${pkgname}/tarball/${_commit}"
        "https://github.com/${_ghorg}/${pkgname}/releases/download/${_stackver}/${_stackname}.tar.gz"
       )
sha256sums=('27de126afd7a8d35da9ea86acdcc36e29744ea80e8cb85bd400308adc4a50110'
            'fffb6996312be9c45227aea2fbde4b0ed71c9e9cb52aeefb78f585e3ddd14e9f')

prepare() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${_ghorg}-${pkgname}-${_commit}"

  stack update
  stack setup
}

build() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${_ghorg}-${pkgname}-${_commit}"

  stack --local-bin-path stack-bins install
}

check() {
  true
}

package() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${_ghorg}-${pkgname}-${_commit}"

  install -d -m755 "${pkgdir}/usr/bin"
  install -m755 stack-bins/stack "${pkgdir}/usr/bin/stack"

  install -d -m755 "${pkgdir}/usr/share/dock/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/dock/${pkgname}/LICENSE"

  # Shell completion?
  # stack --bash-completion-script /usr/bin/stack
  # stack --zsh-completion-script /usr/bin/stack
}
