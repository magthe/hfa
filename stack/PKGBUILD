# Maintainer: Magnus Therning <magnus@therning.org>

_stackver=v2.1.3
_stackname=stack-2.1.3-linux-x86_64-static
_ghorg=commercialhaskell
_commit=65d729e
pkgname=stack
pkgver=2.1.3.1
pkgrel=101
pkgdesc="The Haskell Tool Stack - the binary"
arch=('x86_64')
url='http://www.haskell.org/cabal/'
license=('BSD3')
makedepends=('unzip')
depends=('git' 'ncurses5-compat-libs')
options=('strip' 'staticlibs')
source=("${pkgname}-${pkgver}.tar.gz::https://api.github.com/repos/${_ghorg}/${pkgname}/tarball/${_commit}"
        "https://github.com/${_ghorg}/${pkgname}/releases/download/${_stackver}/${_stackname}.tar.gz"
       )
sha256sums=('6957c5df1cdb115cfd084c3b00a778956e63ebc990d0e40e600f129b7bbbd5a5'
            '4e937a6ad7b5e352c5bd03aef29a753e9c4ca7e8ccc22deb5cd54019a8cf130c')

prepare() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${_ghorg}-${pkgname}-${_commit}"

  stack update
  stack setup
}

build() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${_ghorg}-${pkgname}-${_commit}"

  stack build --dependencies-only
  stack \
        exec ghc-pkg -- list | grep pkgdb | awk '{print "--package-db=" $0}' > db-paths
  stack \
    runhaskell --no-ghc-package-path -- Setup.hs configure \
    $(cat db-paths) \
    --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
  stack \
    runhaskell --no-ghc-package-path -- Setup.hs build
}

check() {
  true
}

package() {
  export PATH="${srcdir}/${_stackname}:${PATH}"
  cd "${srcdir}/${_ghorg}-${pkgname}-${_commit}"

  stack \
    runhaskell --no-ghc-package-path -- Setup.hs copy \
    --destdir="${pkgdir}"

  rm -fr "${pkgdir}/usr/lib"

  # Shell completion?
  # stack --bash-completion-script /usr/bin/stack
  # stack --zsh-completion-script /usr/bin/stack
}
