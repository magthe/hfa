# Maintainer: Magnus Therning <magnus@therning.org>

pkgname=mega-sdist
pkgver=0.4.0.0
pkgrel=101
pkgdesc="Handles uploading to Hackage from mega repos"
arch=('x86_64')
url="https://github.com/snoyberg/${_pkgname}.git"
license=('custom:BSD3')
makedepends=('git' 'stack')
depends=()
options=('strip' 'staticlibs')
source=("https://github.com/snoyberg/mega-sdist/archive/${pkgname}-${pkgver}.tar.gz"
       )
sha256sums=('20c8779ca8d2422ac56b2439a8fd4b3b3b16885cf38fe6eebb4618f56ff66bb8')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"

  stack update
  stack setup
}

build() {
  cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"

  stack build --dependencies-only
  stack \
        exec ghc-pkg -- list | grep pkgdb | awk '{print "--package-db=" $0}' > db-paths
  stack \
        runhaskell --no-ghc-package-path -- Setup.hs configure \
        $(cat db-paths) \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
  stack \
        runhaskell --no-ghc-package-path -- Setup.hs build
}

check() {
  true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"

  stack \
        runhaskell --no-ghc-package-path -- Setup.hs copy \
        --destdir="${pkgdir}"

  rm -fr "${pkgdir}/usr/lib"
}
